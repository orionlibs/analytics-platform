# Format code.
fmt:
    cargo fmt
    taplo fmt
    RUST_LOG=error cargo bundle-licenses --format yaml --previous THIRDPARTY.yaml --output THIRDPARTY.yaml

# Build for your local machine.
build:
    cargo build

# Build release builds.
build-release:
    rustup target add x86_64-unknown-linux-musl
    rustup target add aarch64-unknown-linux-musl
    cross build --release --target x86_64-unknown-linux-musl
    cross build --release --target aarch64-unknown-linux-musl

# Setup a precommit hook.
setup-precommit:
    #!/usr/bin/env bash
    set -euo pipefail
    {
        echo "#!/usr/bin/env bash"
        echo "set -euo pipefail"
        echo "exec just _precommit"
    } > .git/hooks/pre-commit
    chmod +x .git/hooks/pre-commit

# Run the precommit tests.
_precommit:
    cargo fmt --check
    taplo fmt --check
    actionlint
    zizmor .
    RUST_LOG=error cargo bundle-licenses --format yaml --previous THIRDPARTY.yaml | diff - THIRDPARTY.yaml
