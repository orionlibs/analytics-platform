plugins {
    id 'java'
    id 'com.gradleup.shadow' version '8.3.9'
    id 'com.diffplug.spotless' version '6.25.0'
}

// We need this dependency to load the resource JNA shared libraries
dependencies {
    implementation 'net.java.dev.jna:jna:5.18.1'
}

spotless {
    java {
        // Use Google Java Format
        googleJavaFormat()
        // Or use Eclipse formatter
        // eclipse()
        
        // Remove unused imports
        removeUnusedImports()
        
        // Trim trailing whitespace
        trimTrailingWhitespace()
        
        // End files with newline
        endWithNewline()
        
        // Target files
        target 'src/**/*.java'
    }
}

tasks.named('spotlessJava') {
    mustRunAfter(copyAgentShadowJar)
}

clean.doFirst {
    delete fileTree("$projectDir/src/main/resources/agent")
}

task copyAgentShadowJar(type: Copy) {
    dependsOn(':agent:shadowJar')
    from(project(':agent').tasks.shadowJar.outputs.files)
    into("$projectDir/src/main/resources/agent")
    rename { 'agent.zip' }
}

processResources.dependsOn(copyAgentShadowJar)

shadowJar {
    archiveBaseName.set('loader')
    archiveVersion.set('0.1.0')
    archiveClassifier.set('shaded')
    manifest {
        attributes(
            'Main-Class': 'io.opentelemetry.obi.java.Loader',
            'Premain-Class': 'io.opentelemetry.obi.java.Loader',
            'Agent-Class': 'io.opentelemetry.obi.java.Loader',
            'Can-Redefine-Classes': 'true',
            'Can-Retransform-Classes': 'true'
        )
    }
}